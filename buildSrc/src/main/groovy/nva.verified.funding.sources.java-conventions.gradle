plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco-report-aggregation'
}

group 'no.sikt.nva'
version '1.0-SNAPSHOT'

dependencies {
    testImplementation libs.bundles.testing
    implementation libs.bundles.nva
    implementation libs.bundles.jackson
    implementation libs.slf4j.api
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

tasks.named('test') {
    environment "API_HOST", "localhost"
    useJUnitPlatform {
        excludeTags "RemoteTest", "integrationTest", "KarateTest"
    }
    failFast = true
    testLogging {
        events 'skipped', 'passed', 'failed'
        showCauses true
        exceptionFormat "full"
    }
}


pmd {
    toolVersion = '6.55.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle).configureEach{
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}
